/*----------------------
Mode: Blitz
Author: racer_simon
Idea from Blitz Mode 
from Call of Duty Ghosts
----------------------*/

#Extends "Modes/Shootmania/ModeBase.Script.txt"

#Include "TextLib" as TextLib
#Include "MathLib" as MathLib
#Include "Libs/Nadeo/ShootMania/SM.Script.txt" as SM
#Include "Libs/Nadeo/Victory.Script.txt" as Victory
#Include "Libs/Nadeo/ShootMania/Score.Script.txt" as Score
#Include "Libs/Nadeo/ShootMania/Airshot.Script.txt" as Airshot
#Include "Libs/Nadeo/ShootMania/BalancedWeapons.Script.txt" as BalancedWeapons
#Include "Libs/Nadeo/Shootmania/AFK.Script.txt" as AFK
#Include "Libs/Nadeo/Shootmania/SpawnScreen.Script.txt" as SpawnScreen
#Include "Libs/Nadeo/Message.Script.txt" as Message

#Const CompatibleMapTypes "BlitzArena" 
#Const Version			  "2013-12-15"
#Const UITickPeriod	   	   200
#Const ScriptName		  "Blitz.Script.txt"

#Setting S_RespawnTime		   3001   as "RespawnTime (in milliseconds)"
#Setting S_AutoBalance		   True	  as "AutoBalance"
#Setting S_RoundsToWin		   3	  as "RoundsToWin"
#Setting S_RoundGapToWin	   1	  as "RoundGapToWin"
#Setting S_RoundsLimit		   5	  as "RoundsLimit"
#Setting S_TimeLimit		   180    as "TimeLimit (in seconds)"
#Setting S_ClanScoreLimit	   100    as "ClanScoreLimit (set value < 1 to deactivate)"  
#Setting S_LaserOneHitKill     True   as "LaserOneHitKill" 
#Setting S_FriendlyFire	       False  as "FriendlyFire"
#Setting S_PlayerArmor		   200    as "PlayerArmor (100 = 1 bar in HUD, don't use values < 100)"
#Setting S_SpawnProtectionTime 3000   as "SpawnProtectionTime (in milliseconds)"
#Setting S_MaxIdleDuration	   30     as "MaxIdleDuration (in seconds)"
#Setting S_AutoSwitchWeapon    True   as "AutoSwitchWeapon (weapon change by environment)"	
#Setting S_RandomWeapons       False  as "RandomWeapons"
#Setting S_RocketAmmoMax       4	  as "RocketAmmoMax"
#Setting S_LaserAmmoMax 	   1 	  as "LaserAmmoMax"
#Setting S_ArrowAmmoMax	       3      as "ArrowAmmoMax"
#Setting S_NucleusAmmoMax 	   2      as "NucleusAmmoMax"
#Setting S_PoleCaptureDelay    10     as "PoleCaptureDelay (in seconds)"
#Setting S_NbBlueBots		   0	  as "NbBlueBots"
#Setting S_NbRedBots		   0	  as "NbRedBots"

declare Integer[Integer] ClanRoundScore;
declare Integer ClanSide1;
declare Integer ClanSide2;
declare CSmBlockSpawn[Integer] Side1Spawn;
declare CSmBlockSpawn[Integer] Side2Spawn;
declare Integer[Ident] PlayersLastSpawnTime;

***LogVersion***
***
MB_LogVersion(ScriptName, Version);
MB_LogVersion(SM::GetScriptName(), SM::GetScriptVersion());
MB_LogVersion(AFK::GetScriptName(), AFK::GetScriptVersion());
MB_LogVersion(Score::GetScriptName(), Score::GetScriptVersion());
MB_LogVersion(Message::GetScriptName(), Message::GetScriptVersion());
MB_LogVersion(Victory::GetScriptName(), Victory::GetScriptVersion());
MB_LogVersion(SpawnScreen::GetScriptName(), SpawnScreen::GetScriptVersion());
MB_LogVersion(BalancedWeapons::GetScriptName(), BalancedWeapons::GetScriptVersion());
***

***SleepLoop***
***
Message::Loop ();
***
	
***InitServer***
***
//MB_UseLogging = True;
ClanScores[1] = 0;
ClanScores[2] = 0;
UseClans = True;
declare LayerRoundInfo <=> UIManager.UILayerCreate();
***

***StartServer***
***
declare ModeName = "Blitz";
declare ModeRules = "-you can score for your team, if you go to the enemy's pole\n-if somebody has scored, it takes some time for the pole to regenerate and until somebody can score again\n-the team, which reaches the score limit first, wins a round\n-when the time has run out, the team with the highest score wins the round";
SpawnScreen::CreateRules (ModeName, ModeRules);
SpawnScreen::CreateScores();
SpawnScreen::CreateMapInfo();
MB_UseSectionRound = True;
***

***InitMap***
***
Score::MatchBegin(False);	
Airshot::MatchBegin();
BalancedWeapons::MatchBegin();
Victory::MatchBegin();
if(S_AutoBalance) 
	AutoTeamBalance();
Mode::Ladder_OpenMatch_All();
+++MatchBegin+++	
		
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Intro;

ClanScores[1] = 0;
ClanScores[2] = 0;

declare Integer i = 1;
declare Integer j = 1;
foreach (Spawn in BlockSpawns) 
{
    if (Spawn.Order == 1) {
	    Side1Spawn[i] = Spawn;
 		i += 1;
	} else if (Spawn.Order == 2) {
	    Side2Spawn[j] = Spawn;
		j += 1;
	}
}

foreach (Player in AllPlayers)
{
	PlayersLastSpawnTime[Player.Id] = 0;
}
***

***InitRound***
***
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Playing;
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
Score::RoundBegin();	
Airshot::RoundBegin();
BalancedWeapons::RoundBegin();
Victory::RoundBegin();
+++RoundBegin+++
			
// Set Nb of Bots
SetNbFakePlayers (S_NbBlueBots, S_NbRedBots);

ClanRoundScore[1] = 0;
ClanRoundScore[2] = 0;
UIManager.UIAll.BigMessage = "";
		
ClanSide1 = (MB_SectionRoundNb % 2) + 1;
ClanSide2 = 2 - (MB_SectionRoundNb % 2);
InitPoles ();
***

***StartRound***
***
SM::SetupDefaultVisibility ();
declare LatestUITick = Now;
StartTime = Now + S_RespawnTime;
EndTime = StartTime + S_TimeLimit * 1000;
***

***PlayLoop***
***
ProcessEvents ();
ProcessPoleCapturing ();
SM::UnspawnPlayersChangingClan();	
UIManager.UIAll.StatusMessage = "$00f" ^ TextLib::ToText (ClanRoundScore[1]) ^ "$fff : $f00" ^ TextLib::ToText (ClanRoundScore[2]);
foreach(Player in Players) 
{
    Player.IsHighlighted = Player.Armor > 0;
}
AFK::ManageAFKPlayers (S_MaxIdleDuration * 1000, 0);
RespawnPlayers ();
CheckRoundEndConditions ();
***

***EndRound***
***
StartTime = -1;
EndTime = -1;

if (Victory::IsRoundWinner(1)) 
{
	UIManager.UIAll.BigMessage = TextLib::Compose (Teams[0].ColorizedName ^ " wins the round");	
	ClanScores[1] += 1;			
} 
else if (Victory::IsRoundWinner(2)) 
{
	UIManager.UIAll.BigMessage = TextLib::Compose (Teams[1].ColorizedName ^ " wins the round");
	ClanScores[2] += 1;
} 
else 
{
	UIManager.UIAll.BigMessage = TextLib::Compose ("This round is a draw");
}

Victory::SetMatchWinnerFromScore(S_RoundsToWin, S_RoundGapToWin, S_RoundsLimit);

foreach (Player in AllPlayers)
{
	UnspawnPlayer (Player);
}

UIManager.UIAll.UISequence = CUIConfig::EUISequence::EndRound;
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::ForcedVisible;
			
MB_Sleep (5000);

Score::RoundEnd();			
Airshot::RoundEnd();
BalancedWeapons::RoundEnd();
Victory::RoundEnd();	

CheckMatchEndConditions ();
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
***

***EndMap***
***
UIManager.ResetAll();
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Outro;

declare WinnerClan = -1;
if ( Victory::IsMatchWinner(1) ) 
{
	UIManager.UIAll.BigMessage = Teams[0].ColorizedName ^ " wins the match";
	WinnerClan = 1;
} 
else if (  Victory::IsMatchWinner(2) ) 
{
	UIManager.UIAll.BigMessage = Teams[1].ColorizedName ^ " wins the match";
	WinnerClan = 2;
}
		
MB_Sleep(6*1000);

if(WinnerClan != -1)
{
	// Reset score clans
	foreach (Score in Scores) { declare Integer Clan for Score; Clan = 0; }	
	foreach (Player in Players) {
		if (Player.Score == Null) continue;
		declare Integer Clan for Player.Score;
		Clan = Player.CurrentClan;				
	}
	// Sort scores by clan
	declare ClanScoresSorted = [1=>CSmScore[], 2=>CSmScore[]];	
	foreach (Score in Scores) {
		Score.LadderRankSortValue = 1000;
		declare Integer Clan for Score;
		if (Clan != 1 && Clan != 2) continue;
		ClanScoresSorted[Clan].add(Score);				
	}	
	declare Shift = [1=>1, 2=>1];
	Shift[3 - WinnerClan] = 2;			
	foreach(Index => Score in ClanScoresSorted[1]) {Score.LadderRankSortValue = Index * 2 + Shift[1];}
	foreach(Index => Score in ClanScoresSorted[2]) {Score.LadderRankSortValue = Index * 2 + Shift[2];}			
	
	Mode::Ladder_CloseMatch();
} 
else 
	Mode::Ladder_CancelMatch();
		
+++EndMatch+++
Score::MatchEnd();
Airshot::MatchEnd();
BalancedWeapons::MatchEnd();
Victory::MatchEnd();	

UIManager.UIAll.BigMessage = "";
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Podium;				

MB_Sleep(10*1000);
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
UIManager.UIAll.UILayers.clear();
MB_Sleep(1*1000);
	
MatchEndRequested = False;
***

***EndServer***
***
SpawnScreen::DestroyRules ();
SpawnScreen::DestroyScores ();
SpawnScreen::DestroyMapInfo( );
UIManager.UILayerDestroy(LayerRoundInfo);
***

//--------------------------------------------
//------------------FUNCTIONS-----------------
//--------------------------------------------
	
Void RespawnPlayers ()
{
	foreach (Player in AllPlayers) 
	{
		if ((Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned) && (!Player.RequestsSpectate)) 
		{
			// Set Player ArmorMax and LastSpawnTime
			Player.ArmorMax = S_PlayerArmor;
			PlayersLastSpawnTime[Player.Id] = Now + S_RespawnTime;
			
			// Set Player Weapon
			if (S_RandomWeapons)
			{
				declare Integer LuckyNumber = MathLib::Rand (1,4);
				switch (LuckyNumber)
				{
					case 1:
						SetPlayerWeapon (Player, CSmMode::EWeapon::Rocket, S_AutoSwitchWeapon);
					case 2:
						SetPlayerWeapon (Player, CSmMode::EWeapon::Laser, S_AutoSwitchWeapon);
					case 3:
						SetPlayerWeapon (Player, CSmMode::EWeapon::Arrow, S_AutoSwitchWeapon);
					case 4:
						SetPlayerWeapon (Player, CSmMode::EWeapon::Nucleus, S_AutoSwitchWeapon);
					default:
						SetPlayerWeapon (Player, CSmMode::EWeapon::Rocket, S_AutoSwitchWeapon);					
				}
			}
			else
				SetPlayerWeapon (Player, CSmMode::EWeapon::Rocket, S_AutoSwitchWeapon);
			
			// Set Player AmmoMax
			SetPlayerAmmoMax (Player, CSmMode::EWeapon::Rocket, S_RocketAmmoMax);
			SetPlayerAmmoMax (Player, CSmMode::EWeapon::Laser, S_LaserAmmoMax);
			SetPlayerAmmoMax (Player, CSmMode::EWeapon::Arrow, S_ArrowAmmoMax);
			SetPlayerAmmoMax (Player, CSmMode::EWeapon::Nucleus, S_NucleusAmmoMax);
			
			// Spawn Player
			if (Player.RequestedClan == ClanSide1) 							
				SM::SpawnPlayer (Player, Player.RequestedClan, Player.ArmorMax, Side1Spawn[MathLib::Rand ( 1, Side1Spawn.count)], Now + S_RespawnTime);
			else if (Player.RequestedClan == ClanSide2) 							
				SM::SpawnPlayer (Player, Player.RequestedClan, Player.ArmorMax, Side2Spawn[MathLib::Rand ( 1, Side2Spawn.count)], Now + S_RespawnTime);
		}	
	}
}

Void ProcessEvents ()
{
	foreach(Event, PendingEvents) 
	{
		if( Event.Type == CSmModeEvent::EType::OnHit ) 
		{				
	        if ( Event.Shooter == Null || Event.Victim == Null || Event.Shooter == Event.Victim ) 
			    Discard(Event);
			else if ((UseClans) && (Event.Shooter.CurrentClan == Event.Victim.CurrentClan) && (Now > PlayersLastSpawnTime[Event.Victim.Id] + S_SpawnProtectionTime)) 
			{
				if (S_FriendlyFire)
				{
					if(Event.Damage > 0) {
						if (!S_LaserOneHitKill)
							Event.Damage = 100;
						Score::AddPoints( Event.Shooter, -1);									
						Airshot::OnHit(Event.Shooter, Event.Victim);
					}
					PassOn(Event);
				}
				else
					Discard(Event);
		    }
			else if (Now > PlayersLastSpawnTime[Event.Victim.Id] + S_SpawnProtectionTime)
			{			
    			if(Event.Damage > 0) {
				    if (!S_LaserOneHitKill)
					    Event.Damage = 100;
				    Score::AddPoints( Event.Shooter, 1);									
				    Airshot::OnHit(Event.Shooter, Event.Victim);
				}
				PassOn(Event);
			}
		}
		else if (Event.Type == CSmModeEvent::EType::OnArmorEmpty) 
		{		
			BalancedWeapons::OnOut(Event.Shooter, Event.Victim);
		    PassOn(Event);												
		}
		else if (Event.Type == CSmModeEvent::EType::OnPlayerAdded)
		{
			PlayersLastSpawnTime[Event.Player.Id] = 0;
			PassOn (Event);
		}
		else if (Event.Type == CSmModeEvent::EType::OnPlayerRemoved)
		{
			if (PlayersLastSpawnTime.removekey (Event.Player.Id))
				log (Event.Player.Login ^ " left");
			PassOn (Event);
		}
		else 
			PassOn(Event);
	}
}

Void CheckRoundEndConditions ()
{	
	if (S_ClanScoreLimit > 0)
	{ 
		for (i, 1, 2)
		{
			if (ClanRoundScore[i] >= S_ClanScoreLimit)
			{
				Victory::SetRoundWinnerIfNoWinner (i);
				MB_StopRound = True;
			}
		}
	}
	
		
	if(Now > EndTime) 
	{
		if (ClanRoundScore[1] > ClanRoundScore[2])
		{
		    Victory::SetRoundWinnerIfNoWinner (1);
		}
		else if (ClanRoundScore[1] < ClanRoundScore[2])
		{
		    Victory::SetRoundWinnerIfNoWinner (2);
		}
		else
		{
		    Victory::SetRoundDrawIfNoWinner ();
		}
		MB_StopRound = True;
	}
}

Void CheckMatchEndConditions ()
{
	if(!Victory::NoMatchWinner() || MatchEndRequested) 
		MB_StopMatch = True;
}

Void InitPoles ()
{
	foreach (Pole in BlockPoles) 
	{
		Pole.Gauge.Max = S_PoleCaptureDelay * 1000 + 1;
		Pole.Gauge.Value = 0;
		if (Pole.Order == 1)
			Pole.Gauge.Clan = ClanSide1;
		else if (Pole.Order == 2)
			Pole.Gauge.Clan = ClanSide2;
	}
}

Void ProcessPoleCapturing ()
{
	foreach (Pole in BlockPoles)
	{
	    UIManager.UIAll.Hud3dMarkers = """<marker label = "Pole" pos = "{{{Pole.Position.X}}} {{{Pole.Position.Y + 25.}}} {{{Pole.Position.Z}}}"/>""";
		if (Pole.Gauge.Value >= Pole.Gauge.Max - 1)
		{
			Pole.Gauge.Speed = 0;
			declare CSmPlayer[Integer] PlayersInPoleSector;
			declare Integer i = 1;
			foreach (Id in Pole.Sector.PlayersIds)
			{
				if (AllPlayers[Id].CurrentClan != Pole.Gauge.Clan)
				{
					PlayersInPoleSector[i] = AllPlayers[Id];
					i += 1;
				}
			}
			declare Boolean Scored = False;
			foreach (Player in PlayersInPoleSector)
			{
				if (!Scored)
				{
					UnspawnPlayer (Player);
					if ((!Player.IsFakePlayer) && (!Player.IsBot))
						Score::AddPoints (Player, 1);
					ClanRoundScore[Player.CurrentClan] += 1;
					Scored = True;
					Pole.Gauge.Value = 0;
				}
			}				
		}
		else 
			Pole.Gauge.Speed = 1;
	}
}